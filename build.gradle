buildscript {
    ext {
        javaVersion = 1.8
        springBootVersion = '1.4.3.RELEASE'
        //springCloudVersion = 'Brixton.SR3'
    }

    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11'
        classpath 'org.asciidoctor:asciidoctorj-diagram:1.5.0'
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.1'
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id 'com.jfrog.artifactory' version '4.4.9'
    id 'com.github.ben-manes.versions' version '0.13.0'
    id 'org.asciidoctor.convert' version '1.5.3'
}

apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'
apply plugin: 'org.springframework.boot'

sourceCompatibility = "${javaVersion}"
targetCompatibility = "${javaVersion}"

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://maven.repository.redhat.com/ga' }
}

dependencies {
    // Spring
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-data-rest')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.security.oauth:spring-security-oauth2:2.0.12.RELEASE')
    compile('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    compile('org.springframework.boot:spring-boot-starter-undertow')
    compile('org.springframework.hateoas:spring-hateoas')

    compile('org.projectlombok:lombok')

    // Commons
    compile('org.apache.commons:commons-lang3:3.5')

    // YAML support
    compile('com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.8.5')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.5')
    compile('org.projectodd:yaml-schema:0.2')

    // Test
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.spockframework:spock-spring:1.0-groovy-2.4')
    testCompile('org.springframework.restdocs:spring-restdocs-core:1.1.2.RELEASE')

    runtime('com.h2database:h2')
}

springBoot {
    executable = true
    buildInfo()
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

docker {
    javaApplication {
        maintainer = 'Willem Cheizoo "w.cheizoo@gmail.com"'
    }
}

asciidoctor {
    outputDir = file('public')
    separateOutputDirs = false
    requires = ['asciidoctor-diagram']
    attributes buildDir: buildDir,
        'source-highlighter': 'highlight.js',
        toc: 'left',
        toclevels: '3',
        numbered: '',
        icons: 'font',
        idseparator: '-',
        sectnums: 'true',
        sectlinks: '',
        sectanchors: '',
        hardbreaks: '',
        docinfo1: 'true'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}
