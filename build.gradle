buildscript {
    ext {
        javaVersion = 1.8

        springBootVersion = '1.5.2.RELEASE'
        //springCloudVersion = 'Brixton.SR3'
        springSecurityOauth2Version = '2.1.0.RELEASE'

        apacheCommonsLangVersion = '3.5'
        asciidoctorjPdfVersion = '1.5.0-alpha.15'
        asciidoctorjDiagramVersion = '1.5.4'
        gradeDockerPluginVersion = '3.0.6'
        itextpdfVersion = '5.5.11'
        jacksonVersion = '2.8.8'
        jettisonVersion = '1.3.8'
        plexusUtilsVersion = '3.0.24'
        springRestDocsVersion = '1.1.2.RELEASE'
        spockSpringVersion = '1.0-groovy-2.4'
        yamlSchemaVersion = '0.2'

        coverallsVersion = '2.6.3'
    }

    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "org.asciidoctor:asciidoctorj-pdf:$asciidoctorjPdfVersion"
        classpath "org.asciidoctor:asciidoctorj-diagram:$asciidoctorjDiagramVersion"
        classpath "com.bmuschko:gradle-docker-plugin:$gradeDockerPluginVersion"
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'com.jfrog.artifactory' version '4.4.15'
    id 'com.github.ben-manes.versions' version '0.14.0'
    id 'org.asciidoctor.convert' version '1.5.3'
}

apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'
apply plugin: 'org.springframework.boot'

sourceCompatibility = "$javaVersion"
targetCompatibility = "$javaVersion"

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://maven.repository.redhat.com/ga' }
    maven { url 'https://repo.itextsupport.com/releases' }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-starter-parent:$springBootVersion"
    }
}

dependencies {
    // Spring
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-data-rest')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile("org.springframework.security.oauth:spring-security-oauth2:$springSecurityOauth2Version")
    compile('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    compile('org.springframework.boot:spring-boot-starter-undertow')
    compile('org.springframework.hateoas:spring-hateoas')

    compile('org.projectlombok:lombok')

    // Commons
    compile("org.apache.commons:commons-lang3:$apacheCommonsLangVersion")

    // YAML support
    compile("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")
    compile("org.projectodd:yaml-schema:$yamlSchemaVersion")

    //Documents
    compile("org.codehaus.plexus:plexus-utils:$plexusUtilsVersion")
    compile("org.codehaus.jettison:jettison:$jettisonVersion")

    //iText
    compile("com.itextpdf:itextpdf:$itextpdfVersion")

    // Test
    compile('org.springframework.boot:spring-boot-starter-test')
    testCompile("org.spockframework:spock-spring:$spockSpringVersion")
    testCompile("org.springframework.restdocs:spring-restdocs-core:$springRestDocsVersion")

    runtime('com.h2database:h2')
}

springBoot {
    executable = true
    buildInfo()
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

docker {
    javaApplication {
        maintainer = 'Willem Cheizoo "w.cheizoo@gmail.com"'
    }
}

asciidoctor {
    outputDir = file('public')
    separateOutputDirs = false
    requires = ['asciidoctor-diagram']
    attributes buildDir: buildDir,
        'source-highlighter': 'highlight.js',
        toc: 'left',
        toclevels: '3',
        numbered: '',
        icons: 'font',
        idseparator: '-',
        sectnums: 'true',
        sectlinks: '',
        sectanchors: '',
        hardbreaks: '',
        docinfo1: 'true'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}
